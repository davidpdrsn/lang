WHITESPACE = _{ " " | "\n" | "\r" | "\t" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

program = { SOI ~ function+ ~ EOI }

function = {
  "fn"
  ~ identifier
  ~ "(" ~ parameters ~ ")"
  ~ "->" ~ type_
  ~ "{" ~ function_body ~ "}"
}

type_ = { identifier }

function_body = { statement* }

statement = { statement_inner ~ ";" }
statement_inner = _{
  function_call
  | return_statement
  | variable_binding
}

return_statement = { "return" ~ expression }

variable_binding = { "let" ~ identifier ~ "=" ~ expression }

parameters = { ((identifier ~ ","?) ~ ","?)* }
arguments = { ((expression ~ ","?) ~ ","?)* }

expression = {
  string
  | boolean
  | integer
  | function_call
  | identifier
}

function_call = { identifier ~ "(" ~ arguments ~ ")" }

string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

integer = { ASCII_DIGIT+ }

boolean = { "true" | "false" }

identifier = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
